package algo::Algo;

use strict;
use warnings;
use Data::Dumper;
use util::blastf;

my $debug = 0;
my $GUPPY_SING = 1;

sub new {
  my $class = shift;
  my $self  = {@_};
  bless( $self, $class );
  $self->_init;
  return $self;
}

sub config  { my $self = shift; return $self->{config}; }
sub db_conn { my $self = shift; return $self->{db_conn}; }

###
### Initialize look up table for consistent hits.
### Hash value for lookup table is store in $self->{lookup}
###
sub _init {
  my $self = shift;

  my $config = $self->config;

  if ( !defined( $self->{blastout} ) ) {
    $self->{blastout} = 0;
  }

  my $hits = 3;
  if ( defined( $self->{count} ) ) {
    $hits = $self->{count};
  }

  $debug && print "_init: Ready for util::blastf::getLookupTable\n";
  $self->setLengths();
  $self->{lookup} = util::blastf::getLookupTable( $config, $self->db_conn );
  $debug && print STDERR "_init: \$self->{lookup}=\n".Dumper($self->{lookup})."End of \$self->{lookup}\n";
  for my $k (keys %{$self->{lookup}}) {
    $debug && print "_init: $k = $self->{lookup}->{$k}\n";
  }

  $self->{blastdb} =
    join( '/', $config->getValue("blastdir"), $config->getValue("blastdb") );

  $self->{formatdb} = join( ' ',
    $config->getValue("formatdb"),
    "-dbtype nucl -parse_seqids -hash_index -in",
    $self->{blastdb} );

  if (defined($config->getValue("blastall")) && defined($config->getValue("blastpath"))) {
  $self->{blastall} = join( ' ',
    $config->getValue("blastall"),
    "blastall -p blastn -m 8 -e 1 -F F -v $hits -b $hits -g F -d",
    $self->{blastdb},
    "--path " . $config->getValue("blastpath"),
    "-i <INPUTFILE>" );
  }

  $self->{classifierDir} = join( '/',
    $config->getValue("TempDir"),
    $config->getValue("ClassifierDir") );

  $self->{taxit} = join( ' ',
    $config->getValue("taxit"),
    "create -l",
    $config->getValue("Type"),
    "-P",
    $config->getValue("HRefpkg"),
    "--aln-fasta",
    $config->getValue("HAlign"),
    "--tree-stats",
    join( '/', $self->{classifierDir}, $config->getValue("HStat") ),
    "--tree-file",
    join( '/', $self->{classifierDir}, $config->getValue("HTree") ) );

  $self->{pplacer} = join( ' ',
    $config->getValue("pplacer") . "/pplacer",
    "-c", $config->getValue("HRefpkg"), "--out-dir" );

  $self->{guppy} = join( ' ', $config->getValue("pplacer") . "/guppy", "tog" );

  $self->{node_lookup} = join( '/',
    $config->getValue("TempDir"),
    $config->getValue("ClassifierDir"),
    $config->getValue("Node_lookup") );

  $self->{lower_threshold} = $config->getValue("Lower_Threshold");
  $self->{upper_threshold} = $config->getValue("Upper_Threshold");

  $self->{input_fasta} =
    join( '/', $self->{classifierDir}, $config->getValue("blastdb") );
}

sub taxit {
  my $self = shift;
  my ($accession) = @_;

  my $refpkg = $self->config->getValue("HRefpkg");
  if ( -e $refpkg ) {
    system("/bin/rm -fr $refpkg");
    my $status = $?;
    die "Could not remove reference package for $accession ($status)\n"
      if ($status);
  }
  my $taxit =
    $self->{taxit} . " >$accession.taxit.std 2>$accession.taxit.err";
  print "$accession = $taxit\n";
  system("$taxit");
  my $status = $?;
  die "Failed to run taxit for $accession ($status)\n" if ($status);
}

sub blastdb {
  my $self = shift;
  my ($accession) = @_;

  my $blastdb    = $self->{blastdb};
  my $inputFasta = $self->{input_fasta};

  my $blastdir = $self->config->getValue("blastdir");
  if ( -e $blastdir ) {
    system("/bin/rm -fr $blastdir");
    my $status = $?;
    die "Could not remove blastdir for $accession ($status)\n"
      if ($status);
  }
  system("mkdir -p -m 0775 $blastdir");

  open( LOG, ">> $blastdb.log" )
    or die "Couldn't able to open the log file: $blastdb.log\n";
  print LOG "Created blastdir ($blastdir).\t\t" . localtime() . "\n";

  print LOG "Copying blastdb ($inputFasta to $blastdb).\t\t"
    . localtime() . "\n";
  system("cp $inputFasta $blastdb");

  print LOG "Running formatdb on $blastdb.\t\t" . localtime() . "\n";
  my $formatdbCmd =
    $self->{formatdb} . " > $accession.formatdb.std 2> $accession.formatdb.err";
  system($formatdbCmd);
  my $status = $?;
  die "Failed to run formatdb for $accession ($status)\n" if ($status);
  print LOG "Finished formatdb on $blastdb.\t\t" . localtime() . "\n";
  close(LOG);
}

sub tree {
  my $self = shift;
  my ( $accession, $seq ) = @_;

  print "tree:\n";
  my $sequenceFile = "$accession.fasta";
  open( SEQ, ">$sequenceFile" ) or die "Unable to open $sequenceFile:  $@\n";
  print SEQ ">$accession\n";
  print SEQ "$seq";
  close SEQ;

  my $guppyCmd   = $self->{guppy};
  my $pplacerCmd = $self->{pplacer};

  $pplacerCmd .=
    " . $sequenceFile >$accession.pplacer.std 2>$accession.pplacer.err";
  print "Algo::Tree: $pplacerCmd\n";
  system($pplacerCmd);
  my $status = $?;
  die "Failed to run pplacer for $accession ($status)\n" if ($status);

  $sequenceFile =~ s/\.fasta$/.jplace/;
  my $treeData = "";
  if ( -e $sequenceFile ) {
    # For ViPR genotype and HCV, use sing over tog -Jun 21, 2017
    $guppyCmd =~ s/tog/sing/ if ($GUPPY_SING);
    $guppyCmd .=
      " $sequenceFile >$accession.guppy.std 2>$accession.guppy.err";
    print "Algo::Tree: $guppyCmd\n";
    system($guppyCmd);
    my $status = $?;
    die "Failed to run guppy for $accession ($status)\n" if ($status);
    $sequenceFile =~ s/\.jplace$/.tog.tre/;
    $sequenceFile =~ s/tog/sing/ if ($GUPPY_SING);
    if ( !-e $sequenceFile ) {
      warn "$accession Accession guppy error\n";
    }
    else {
      ###
      ### The the unified tree
      ###
      open( TREEDATA, "<$sequenceFile" )
        or die "Unable to open $sequenceFile:  $@\n";
      while (<TREEDATA>) {
        chomp $_;
        $treeData .= $_;
      }
      close TREEDATA;
    }
  }
  else {
    warn "$accession Accession pplacer error\n";
  }
  if ( -e $sequenceFile ) {
    return ( $sequenceFile, $treeData );
  }
  else {
    return ( undef, undef );
  }
}

sub getLookup {
  my $self = shift;
  my ($accession) = @_;
  return $self->{lookup}->{$accession};
}

sub setLengths {
  my $self        = shift;
  my $config      = $self->config;
  my $lengthsFile = $config->getValue("lengthsFile");
  $self->{lengths} = {};
  if (!defined($lengthsFile) || $lengthsFile eq "") {
    ###
    ### Lengths File is not defined so it will be empty
    ###
    return;
  }
  open( LENGTHS, "< $lengthsFile" )
    or die "Couldn't able to open the lengths file: lengthFile\n";
  while (<LENGTHS>) {
    chomp $_;
    my ( $accession, $length ) = split( /\t/, $_ );
    $self->{lengths}->{$accession} = $length;
  }
  close LENGTHS;
}

sub getLength {
  my $self = shift;
  my ($accession) = @_;
  return $self->{lengths}->{$accession};
}

sub getClassification {
  my $self = shift;
  my (@params) = @_;
  #######################
  ### Abstract Method ###
  #######################
  return undef;
}

1;
